/* Copyright Â© 2017-2020 ABBYY Production LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
--------------------------------------------------------------------------------------------------------------*/

layout(std430, binding=1) readonly buffer Matrix { float matrix[]; };
layout(std430, binding=2) writeonly buffer Result { float result[]; };
layout(std430, binding=3) writeonly buffer MaxIndices { int indices[]; };
shared vec2 bufferValuesIndices[gl_WorkGroupSize.x * gl_WorkGroupSize.y];


void main()
{
    int bufferIndex = LOCAL_ID_Y * int(gl_WorkGroupSize.x) + LOCAL_ID_X;
    bufferValuesIndices[bufferIndex] = vec2(FLT_MAX, 0);
    
    int row = ID_Y;
    int column = ID_X;

    if( row < P.matrixHeight && column < P.matrixWidth ) {
		int matrixStart = row * P.matrixWidth + column;
		int matrixEnd = P.matrixHeight * P.matrixWidth;

		int matrixIndex = matrixStart;
		int maxIndex = row;
		while( matrixIndex < matrixEnd ) {
			if( bufferValuesIndices[bufferIndex].x > matrix[matrixIndex] ) {
				bufferValuesIndices[bufferIndex].x = matrix[matrixIndex];
				bufferValuesIndices[bufferIndex].y = maxIndex;
			}
			matrixIndex += int(gl_WorkGroupSize.y) * P.matrixWidth;
			maxIndex += int(gl_WorkGroupSize.y);
		}
	}

	barrier();

	uint s = 1;
	while( s * 2 < gl_WorkGroupSize.y ) {
		s = s * 2;
	}
       
	for( uint i = s; i >= 1; i = i >> 1 ) {
		if( LOCAL_ID_Y < i && LOCAL_ID_Y + i < gl_WorkGroupSize.y ) {
			if( bufferValuesIndices[bufferIndex].x >= bufferValuesIndices[bufferIndex + i * gl_WorkGroupSize.x].x ) {
				bufferValuesIndices[bufferIndex] = bufferValuesIndices[bufferIndex + i * gl_WorkGroupSize.x];
			}
		}
		barrier();
	}

    if( LOCAL_ID_Y == 0 && column < P.matrixWidth ) {
        result[column] = bufferValuesIndices[bufferIndex].x;
		indices[column] = int(bufferValuesIndices[bufferIndex].y);
    }
}
