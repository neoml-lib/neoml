/* Copyright Â© 2017-2020 ABBYY Production LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
--------------------------------------------------------------------------------------------------------------*/

layout(std430, binding=1) readonly buffer Source { vec4 source[]; };
layout(std430, binding=2) readonly buffer Filter { vec4 flt[]; };
layout(std430, binding=3) readonly buffer FreeTerm { float freeTerm[]; };
layout(std430, binding=4) writeonly buffer Result { float result[]; };

void perform1(in int b, in int h, in int w, in int c, in bool isProc)
{
	int inputChannels4 = ( P.inputChannels + 3 ) / 4;

	int inputHStart = h * P.stride.y - P.padding.y;
	int inputHEnd = inputHStart + P.filterHeight * P.dilation.y;
	int inputWStart = w * P.stride.x - P.padding.x;
	int inputWEnd = inputWStart + P.filterWidth * P.dilation.x;

	int inputYStart = 0;
	int inputXStart = b * P.inputWidth;
	int inputXSize = P.inputWidth * P.batchSize;

	int filterYStart = 0;
	int filterXStart = c * P.filterWidth;
	int filterXSize = P.filterWidth * P.filterCount;
	
	vec4 acc = vec4(0);
	for(int k = 0; k < inputChannels4; ++k) {
		int filterY = filterYStart;
		for(int j = inputHStart; j < inputHEnd; j += P.dilation.y) {
			int filterX = filterXStart;
			for(int i = inputWStart; i < inputWEnd; i += P.dilation.x) {
				if(isProc && j >= 0 && j < P.inputHeight && i >= 0 && i < P.inputWidth) {
					vec4 srcVal = source[(inputYStart + j) * inputXSize + (inputXStart + i)];
					vec4 fltVal = flt[filterY * filterXSize + filterX];
					MULT_ADD_OPER(acc, srcVal, fltVal);
				}
				++filterX;
			}
			++filterY;
		}
	
		inputYStart += P.inputHeight;
		filterYStart += P.filterHeight;
	}
	
	if(isProc) {
		float res = acc.x + acc.y + acc.z + acc.w;
		if(P.isFreeTerm != 0) {
			res += freeTerm[c];
		}
		
		result[((b * P.height + h) * P.width + w) * P.filterCount + c] = res;
	}
}

void main()
{
	int w = ID_X;
	int b = w / P.width;
	w -= b * P.width;
	int c = ID_Y;
	int channelCount = P.filterCount - P.startChannel;
	int h = c / channelCount;
	c -= h * channelCount;
	c += P.startChannel;

	bool isProc = b < P.batchSize && h < P.height;
	perform1(b, h, w, c, isProc);
}
