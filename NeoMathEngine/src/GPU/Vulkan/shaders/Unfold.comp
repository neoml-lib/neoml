/* Copyright Â© 2017-2020 ABBYY Production LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
--------------------------------------------------------------------------------------------------------------*/

layout(std430, binding=1) readonly buffer Images { float images[]; };
layout(std430, binding=2) writeonly buffer Matrices { float matrices[]; };

void main()
{
	int outX = ID_Y % P.outWidth;
	int outY = ID_Y / P.outWidth;
	int b = outY / P.outHeight;
	outY = outY % P.outHeight;

	int c = ID_X;

	if( c >= P.channels || b >= P.batchSize ) {
		return;
	}

	int matrixWidth = P.channels * P.filterWidth * P.filterHeight;
	int matrixIndex = ID_Y * matrixWidth + ID_X;
	int inputBatchOffset = b * P.imageHeight * P.imageWidth * P.channels;

	for( int filterY = 0; filterY < P.filterHeight; ++filterY ) {
		int inY = outY * P.strideHeight - P.paddingHeight + filterY * P.dilationHeight;
		for( int filterX = 0; filterX < P.filterWidth; ++filterX ) {
			int inX = outX * P.strideWidth - P.paddingWidth + filterX * P.dilationWidth;
			if( inY < 0 || inX < 0 || inY >= P.imageHeight || inX >= P.imageWidth ) {
				matrices[matrixIndex] = 0;
			} else {
				int inputIndex = inputBatchOffset + ( inY * P.imageWidth + inX ) * P.channels + c;
				matrices[matrixIndex] = images[inputIndex];
			}
			matrixIndex += P.channels;
		}
	}
}
