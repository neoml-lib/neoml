/* Copyright Â© 2017-2021 ABBYY Production LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
--------------------------------------------------------------------------------------------------------------*/

layout(std430, binding=1) readonly buffer WX { float wx[]; };
layout(std430, binding=2) readonly buffer U { float u[]; };
layout(std430, binding=3) writeonly buffer H { float h[]; };

void main()
{
	int index = ID_VEC;
	if( index < P.batchSize * P.objectSize ) {
		const int batch = index / P.objectSize;
		const int elem = index % P.objectSize;

		const float weight = u[elem];
		const int stepOffset = P.reverse != 0 ? -P.batchSize * P.objectSize : P.batchSize * P.objectSize;

		int currOffset = index;
		if( P.reverse != 0 ) {
			currOffset += ( P.sequenceLength - 1 ) * P.batchSize * P.objectSize;
		}

		float currRes = wx[currOffset];
		currRes = currRes > 0.f ? currRes : 0.f;
		h[currOffset] = currRes;

		for( int step = 0; step < P.sequenceLength - 1; ++step ) {
			currOffset += stepOffset;
			currRes = wx[currOffset] + weight * currRes;
			currRes = currRes > 0.f ? currRes : 0.f;
			h[currOffset] = currRes;
		}
	}
}
